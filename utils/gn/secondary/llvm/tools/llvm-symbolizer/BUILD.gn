import("//llvm/tools/binutils_symlinks.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("addr2line") {
    deps = [
      ":llvm-symbolizer",
    ]
    source = "llvm-symbolizer"
    output = "$root_out_dir/bin/addr2line"
  }
}

# //:llvm-symbolizer depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [
    ":llvm-symbolizer",
  ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":addr2line" ]
  }
}

executable("llvm-symbolizer") {
  deps = [
    "//llvm/lib/DebugInfo/DWARF",
    "//llvm/lib/DebugInfo/PDB",
    "//llvm/lib/DebugInfo/Symbolize",
    "//llvm/lib/Demangle",
    "//llvm/lib/Object",
    "//llvm/lib/Support",
  ]
  sources = [
    "llvm-symbolizer.cpp",
  ]
}
