import("//llvm/utils/TableGen/tablegen.gni")

tablegen("WebAssemblyGenInstrInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-instr-info" ]
  td_file = "../WebAssembly.td"
}

tablegen("WebAssemblyGenMCCodeEmitter") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-emitter" ]
  td_file = "../WebAssembly.td"
}

tablegen("WebAssemblyGenRegisterInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-register-info" ]
  td_file = "../WebAssembly.td"
}

tablegen("WebAssemblyGenSubtargetInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-subtarget" ]
  td_file = "../WebAssembly.td"
}

group("tablegen") {
  visibility = [
    ":MCTargetDesc",
    "../InstPrinter",
    "../TargetInfo",
    "../Utils",
  ]
  public_deps = [
    ":WebAssemblyGenInstrInfo",
    ":WebAssemblyGenMCCodeEmitter",
    ":WebAssemblyGenRegisterInfo",
    ":WebAssemblyGenSubtargetInfo",
  ]
}
static_library("MCTargetDesc") {
  output_name = "LLVMWebAssemblyDesc"
  public_deps = [
    ":tablegen",
  ]
  deps = [
    "//llvm/lib/MC",
    "//llvm/lib/Support",
    "//llvm/lib/Target/WebAssembly/InstPrinter",
    "//llvm/lib/Target/WebAssembly/TargetInfo",
  ]
  include_dirs = [ ".." ]
  sources = [
    "WebAssemblyAsmBackend.cpp",
    "WebAssemblyMCAsmInfo.cpp",
    "WebAssemblyMCCodeEmitter.cpp",
    "WebAssemblyMCTargetDesc.cpp",
    "WebAssemblyTargetStreamer.cpp",
    "WebAssemblyWasmObjectWriter.cpp",
  ]
}
