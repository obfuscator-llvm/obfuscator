import("//llvm/utils/TableGen/tablegen.gni")

tablegen("PPCGenAsmWriter") {
  visibility = [ ":InstPrinter" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../PPC.td"
}

static_library("InstPrinter") {
  output_name = "LLVMPowerPCAsmPrinter"
  deps = [
    ":PPCGenAsmWriter",
    "//llvm/lib/MC",
    "//llvm/lib/Support",

    # MCTargetDesc depends on InstPrinter, so we can't depend on the full
    # MCTargetDesc target here: it would form a cycle.
    "//llvm/lib/Target/PowerPC/MCTargetDesc:tablegen",
  ]
  include_dirs = [ ".." ]
  sources = [
    "PPCInstPrinter.cpp",
  ]
}
