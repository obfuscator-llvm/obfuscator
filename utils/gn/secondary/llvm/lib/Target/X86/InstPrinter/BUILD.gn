import("//llvm/utils/TableGen/tablegen.gni")

tablegen("X86GenAsmWriter") {
  visibility = [ ":InstPrinter" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../X86.td"
}

tablegen("X86GenAsmWriter1") {
  visibility = [ ":InstPrinter" ]
  args = [
    "-gen-asm-writer",
    "-asmwriternum=1",
  ]
  td_file = "../X86.td"
}

static_library("InstPrinter") {
  output_name = "LLVMX86AsmPrinter"
  deps = [
    ":X86GenAsmWriter",
    ":X86GenAsmWriter1",
    "//llvm/lib/MC",
    "//llvm/lib/Support",

    # MCTargetDesc depends on InstPrinter, so we can't depend on the full
    # MCTargetDesc target here: it would form a cycle.
    "//llvm/lib/Target/X86/MCTargetDesc:tablegen",
    "//llvm/lib/Target/X86/Utils",
  ]
  include_dirs = [ ".." ]
  sources = [
    "X86ATTInstPrinter.cpp",
    "X86InstComments.cpp",
    "X86InstPrinterCommon.cpp",
    "X86IntelInstPrinter.cpp",
  ]
}
